
#Область ПрограммныйИнтерфейс

// Прибавить к дате время из другой даты.
//
// Параметры:
//  Дата	 - Дата - дата.
//  Время	 - Дата - время.
// 
// Возвращаемое значение:
//  Дата - результат сложения.
//
Функция ПрибавитьВремя(Знач Дата, Знач Время) Экспорт
	
	Час = Час(Время);
	Минута = Минута(Время);
	Секунда = Секунда(Время);
	
	Возврат Дата + СекундВЧасе() * Час + СекундВМинуте() * Минута + Секунда;
	
КонецФункции

// Добавить день.
//
// Параметры:
//  Дата		 - Дата - первоначальная дата.
//  ЧислоДней	 - Число - количество дней для добавления.
// 
// Возвращаемое значение:
//  Дата - результат добавления.
//
Функция ДобавитьДень(Знач Дата, Знач ЧислоДней) Экспорт 
	Возврат Дата + ЧасовВСутках() * СекундВЧасе() * ЧислоДней;
КонецФункции

// Добавить год.
//
// Параметры:
//  Дата	 - Дата - первоначальная дата.
//  ЧислоЛет - Число - количество лет для добавления.
// 
// Возвращаемое значение:
//  Дата - результат добавления.
//
Функция ДобавитьГод(Знач Дата, Знач ЧислоЛет) Экспорт
	Возврат ДобавитьМесяц(Дата, МесяцевВГоду() * ЧислоЛет);
КонецФункции

// Начало следующего дня.
//
// Параметры:
//  Дата - Дата - дата.
// 
// Возвращаемое значение:
//  Дата - начало следующего дня.
//
Функция НачалоСледующегоДня(Знач Дата) Экспорт
	Возврат КонецДня(Дата) + 1;
КонецФункции

// Добавить минуты к дате с учетом графика работы.
//
// Параметры:
//  Дата					 - Дата  - дата.
//  КоличествоМинут			 - Число - количество минут.
//  ГрафикНачалоРаботы		 - Дата  - начало графика работы.
//  ГрафикОкончаниеРаботы	 - Дата  - окончание графика работы.
// 
// Возвращаемое значение:
//  Дата - результат.
//
Функция ДобавитьМинутыКДатеСГрафиком(Знач Дата, Знач КоличествоМинут, Знач ГрафикНачалоРаботы, Знач ГрафикОкончаниеРаботы) Экспорт
	
	РазницаВСекундах = КоличествоМинут * СекундВМинуте();
	ВремяРаботыВСекундах = ГрафикОкончаниеРаботы - ГрафикНачалоРаботы;
	
	Если ВремяРаботыВСекундах <= 0 Тогда
		ВызватьИсключение "Неправильный график работы";
	КонецЕсли;
	
	НачалоРаботыВДатуНачала = ПрибавитьВремя(НачалоДня(Дата), ГрафикНачалоРаботы);
	КонецРаботыВДатуНачала = ПрибавитьВремя(НачалоДня(Дата), ГрафикОкончаниеРаботы);
	
	ОстатокДоКонцаРаботы = КонецРаботыВДатуНачала - Дата;
	
	Если РазницаВСекундах > ОстатокДоКонцаРаботы Тогда
		
		ОстатокПослеОкончанияРаботы = РазницаВСекундах - ОстатокДоКонцаРаботы;
		
		РазницаВДняхРаботы = Цел(ОстатокПослеОкончанияРаботы / ВремяРаботыВСекундах);
		ОстатокПослеВычитанияДней = ОстатокПослеОкончанияРаботы - РазницаВДняхРаботы * ВремяРаботыВСекундах;
		
		Результат = ДобавитьДень(НачалоРаботыВДатуНачала, РазницаВДняхРаботы + 1) + ОстатокПослеВычитанияДней;
		
	Иначе
		Результат = Дата + РазницаВСекундах;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавить минуту.
//
// Параметры:
//  Дата            - Дата - первоначальная дата.
//  КоличествоМинут - Число - количество минут для добавления.
// 
// Возвращаемое значение:
//  Дата - результат добавления.
//
Функция ДобавитьМинуту(Знач Дата, Знач КоличествоМинут) Экспорт 
	Возврат Дата + СекундВМинуте() * КоличествоМинут;
КонецФункции

// Получить пустую дата.
// 
// Возвращаемое значение:
//  Дата - дата.
//
Функция ПустаяДата() Экспорт
	Возврат '00010101';
КонецФункции

// Добавить час.
//
// Параметры:
//  Дата            - Дата - первоначальная дата.
//  КоличествоЧасов - Число - количество часов для добавления.
// 
// Возвращаемое значение:
//  Дата - результат добавления.
//
Функция ДобавитьЧас(Знач Дата, Знач КоличествоЧасов) Экспорт 
	Возврат Дата + СекундВЧасе() * КоличествоЧасов;
КонецФункции

// Число секунд от начала Unix эпохи.
//
// Параметры:
//  Дата		 - Дата	 - дата.
//  ЧасовойПояс	 - Строка - Идентификатор часового пояса, к которому относится время (см. УниверсальноеВремя).
// 
// Возвращаемое значение:
//  Число - число секунд.
//
Функция ДатаВTimestamp(Знач Дата, Знач ЧасовойПояс = "") Экспорт
	
	Если ПустаяСтрока(ЧасовойПояс) Тогда
		ЧасовойПояс = "GMT+0";
	КонецЕсли;
	
	Возврат УниверсальноеВремя(Дата, ЧасовойПояс) - НачалоЭпохиLinux();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НачалоЭпохиLinux()
	Возврат '19700101';
КонецФункции

Функция СекундВЧасе()
	Возврат 3600;
КонецФункции

Функция СекундВМинуте()
	Возврат 60;
КонецФункции

Функция ЧасовВСутках()
	Возврат 24;
КонецФункции

Функция МесяцевВГоду()
	Возврат 12;
КонецФункции

#КонецОбласти
