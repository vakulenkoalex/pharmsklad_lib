
#Область ПрограммныйИнтерфейс

// Заменить запрос в пакете запросов на запрос для замены.
//
// Параметры:
//  Запрос                                - Строка - исходный текст запроса,
//  ПорядковыйНомерЗапросаВПакетеЗапросов - Число - порядковый номер запроса в пакете запросов,
//  ЗапросДляЗамены                       - Строка - текст запроса для замены.
// 
// Возвращаемое значение:
//  Строка - текст запроса с замененным запросом.
//
Функция ЗаменитьТекстЗапроса(Знач Запрос, Знач ПорядковыйНомерЗапросаВПакетеЗапросов, Знач ЗапросДляЗамены) Экспорт
	
	СхемаЗапросаИсточника = Новый СхемаЗапроса();
	СхемаЗапросаИсточника.УстановитьТекстЗапроса(ЗапросДляЗамены);
	
	СхемаЗапросаПриемника = Новый СхемаЗапроса();
	СхемаЗапросаПриемника.УстановитьТекстЗапроса(Запрос);
	ПакетЗапросовПриемника = СхемаЗапросаПриемника.ПакетЗапросов;
	ПакетЗапросовПриемника.Удалить(ПорядковыйНомерЗапросаВПакетеЗапросов);
	
	Счетчик = 0;
	
	Для Каждого ЗапросИсточник Из СхемаЗапросаИсточника.ПакетЗапросов Цикл
		
		ЗапросПриемник = ПакетЗапросовПриемника.Добавить(ТипЗнч(ЗапросИсточник));
		
		Если Тип(ЗапросИсточник) = Тип("ЗапросУничтоженияТаблицыСхемыЗапроса") Тогда
			ЗапросПриемник.ИмяТаблицы = ЗапросИсточник.ИмяТаблицы;
		Иначе
			ЗапросПриемник.УстановитьТекстЗапроса(ЗапросИсточник.ПолучитьТекстЗапроса());
		КонецЕсли;
		
		ПакетЗапросовПриемника.Переместить(ЗапросПриемник, ПорядковыйНомерЗапросаВПакетеЗапросов + Счетчик);
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	Возврат СхемаЗапросаПриемника.ПолучитьТекстЗапроса();
	
КонецФункции

// Заменить запрос в пакете запросов на запрос для замены.
//
// Параметры:
//  Запрос          - Строка - исходный текст запроса,
//  ИмяТаблицы      - Строка - имя временной таблицы для поиска запроса для замены,
//  ЗапросДляЗамены - Строка - текст запроса для замены.
// 
// Возвращаемое значение:
//  Строка - текст запроса с замененным запросом.
//
Функция ЗаменитьТекстЗапросаПоИмениТаблицы(Знач Запрос, Знач ИмяТаблицы, Знач ЗапросДляЗамены) Экспорт
	
	ПорядковыйНомер = Неопределено;
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос);
	ПакетЗапросовПриемника = СхемаЗапроса.ПакетЗапросов;
	
	Для Каждого ЗапросПриемник Из ПакетЗапросовПриемника Цикл
		
		Если ТипЗнч(ЗапросПриемник) <> Тип("ЗапросВыбораСхемыЗапроса") Или ЗапросПриемник.ТаблицаДляПомещения <> ИмяТаблицы Тогда
			Продолжить;
		КонецЕсли;
		
		ПорядковыйНомер = ПакетЗапросовПриемника.Индекс(ЗапросПриемник);
		Прервать;
		
	КонецЦикла;
	
	Если ПорядковыйНомер = Неопределено Тогда
		ВызватьИсключение "Не найден запрос создания временной таблицы " + ИмяТаблицы;
	КонецЕсли;
	
	Возврат ЗаменитьТекстЗапроса(Запрос, ПорядковыйНомер, ЗапросДляЗамены);
	
КонецФункции

#КонецОбласти