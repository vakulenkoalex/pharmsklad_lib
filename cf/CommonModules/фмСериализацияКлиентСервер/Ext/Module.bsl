
#Область ПрограммныйИнтерфейс

#Если Не ВебКлиент Тогда

// Прочитать JSON.
//
// Параметры:
//  СтрокаJSON - Строка - строка для чтения.
// 
// Возвращаемое значение:
//  Произвольный - результат.
//
Функция UnJSON(Знач СтрокаJSON) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Данные = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Данные;
	
КонецФункции

// Сохранить данные в формате JSON.
//
// Параметры:
//  Данные - Произвольный - данные для сохранения.
// 
// Возвращаемое значение:
//  Строка - json представление.
//
Функция JSON(Знач Данные) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаJSON;
	
КонецФункции

// Сохранить ОбъектXDTO в JSON.
// Антибаг платформы с выгрузкой чисел с типом отличным от decimal.
//
// Параметры:
//  ОбъектXDTO - ОбъектXDTO - Объект XDTO.
// 
// Возвращаемое значение:
//  Строка - json представление ОбъектXDTO.
//
Функция СохранитьОбъектXDTOВJson(Знач ОбъектXDTO) Экспорт
	Возврат JSON(фмКонструкторыКлиентСервер.ОбъектXDTOВСтруктуру(ОбъектXDTO));
КонецФункции

// Восстановить ОбъектXDTO из JSON.
//
// Параметры:
//  ПространстваИмен - Строка - URI пространства имен запрашиваемого типа.
//  ИмяТипа			 - Строка - Имя запрашиваемого типа.
//  СтрокаJSON		 - Строка - Строка для чтения.
// 
// Возвращаемое значение:
//  ОбъектXDTO - Объект XDTO.
//
Функция ВосстановитьОбъектXDTOИзJson(Знач ПространстваИмен, Знач ИмяТипа, Знач СтрокаJSON) Экспорт
	
	Тип = ФабрикаXDTO.Тип(ПространстваИмен, ИмяТипа);
	Если Тип = Неопределено Тогда
		ВызватьИсключение "Тип XDTO не найден: " + ИмяТипа;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Результат = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON, Тип);
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли

#КонецОбласти