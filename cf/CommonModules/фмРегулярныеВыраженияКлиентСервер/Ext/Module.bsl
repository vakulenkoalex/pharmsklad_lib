
#Область ПрограммныйИнтерфейс

#Если Не ВебКлиент Тогда

// Проверить строку на соответствие регулярному выражению.
//
// Параметры:
//  Строка - Строка - проверяемая строка.
//  Фасет - Строка - регулярное выражение.
// 
// Возвращаемое значение:
//  Булево - если Истина, то строка подходит под регулярное выражение.
//
Функция ПроверитьСтроку(Знач Строка, Знач Фасет) Экспорт
	
	Пакет = 
		"<Model xmlns=""http://v8.1c.ru/8.1/xdto"" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" xsi:type=""Model"">
		|<package targetNamespace=""sample-my-package"">
		|<valueType name=""testtypes"" base=""xs:string"">
		|<pattern>%1</pattern>
		|</valueType>
		|<objectType name=""TestObj"">
		|<property xmlns:d4p1=""sample-my-package"" name=""TestItem"" type=""d4p1:testtypes""/>
		|</objectType>
		|</package>
		|</Model>";
	
	Чтение = Новый ЧтениеXML();
	Чтение.УстановитьСтроку(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Пакет, Фасет));
	Модель = ФабрикаXDTO.ПрочитатьXML(Чтение);
	МояФабрикаXDTO = Новый ФабрикаXDTO(Модель);
	Пакет = МояФабрикаXDTO.Пакеты.Получить("sample-my-package");
	Тест = МояФабрикаXDTO.Создать(Пакет.Получить("TestObj"));
	
	Попытка
		Тест.TestItem = Строка;
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

#КонецЕсли

#КонецОбласти
