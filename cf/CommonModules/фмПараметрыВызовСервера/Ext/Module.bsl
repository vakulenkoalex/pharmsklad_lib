
#Область ПрограммныйИнтерфейс

// Прочитать значение параметра.
//
// Параметры:
//  ИмяПараметра        - Строка, СправочникСсылка.фмПараметры - имя параметра или ссылка на предопределенный элемент.
//  ЗначениеПоУмолчанию - Строка, Число, Булево, Дата - значение по умолчанию.
// 
// Возвращаемое значение:
//  Строка, Число, Булево, Дата - значение параметра.
//
Функция ПрочитатьЗначениеПараметра(Знач ИмяПараметра, Знач ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Результат = ЗначениеПоУмолчанию;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	фмПараметрыМассивЗначений.Значение КАК Значение
	|ИЗ
	|	Справочник.фмПараметры КАК фмПараметры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.фмПараметры.МассивЗначений КАК фмПараметрыМассивЗначений
	|		ПО фмПараметры.Ссылка = фмПараметрыМассивЗначений.Ссылка
	|ГДЕ
	|	ИСТИНА";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Параметр", ИмяПараметра);
	Если ТипЗнч(ИмяПараметра) = Тип("Строка") Тогда
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "ИСТИНА", "фмПараметры.Наименование = &Параметр");
	Иначе
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "ИСТИНА", "фмПараметры.Ссылка = &Параметр");
	КонецЕсли;
	МассивЗначений = Новый Массив();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивЗначений.Добавить(Выборка.Значение);
	КонецЦикла;
	Если МассивЗначений.Количество() = 1 Тогда
		Результат = МассивЗначений[0];
	ИначеЕсли МассивЗначений.Количество() > 1 Тогда
		Результат = МассивЗначений;
	Иначе
		// оставим без изменений.
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
