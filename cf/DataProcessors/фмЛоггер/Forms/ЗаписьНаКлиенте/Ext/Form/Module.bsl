
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИерархияСобытия = Параметры.ИерархияСобытия;
	ИмяКлиента = Параметры.ИмяКлиента;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура Ошибка(Знач ИмяСобытия, Знач Сообщение, Знач Данные = Неопределено) Экспорт
	ЗаписатьВЛог("Ошибка", ИмяСобытия, ПреобразоватьСообщение(Сообщение), Данные);
КонецПроцедуры

&НаКлиенте
Процедура Информация(Знач ИмяСобытия, Знач Сообщение, Знач Данные = Неопределено) Экспорт
	ЗаписатьВЛог("Информация", ИмяСобытия, ПреобразоватьСообщение(Сообщение), Данные);
КонецПроцедуры

&НаКлиенте
Процедура Отладка(Знач ИмяСобытия, Знач Сообщение, Знач Данные = Неопределено) Экспорт
	ЗаписатьВЛог("Отладка", ИмяСобытия, ПреобразоватьСообщение(Сообщение), Данные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПреобразоватьСообщение(Знач Сообщение)
	
	Результат = Сообщение;
	
	Если ТипЗнч(Сообщение) = Тип("ИнформацияОбОшибке") Тогда
		Результат =  Новый Структура("КраткоеПредставление, ПодробноеПредставление",
		                             КраткоеПредставлениеОшибки(Сообщение),
		                             ПодробноеПредставлениеОшибки(Сообщение));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьВЛог(Знач ИмяМетода, Знач ИмяСобытия, Знач Сообщение, Знач Данные = Неопределено)
	
	Логгер = Обработки.фмЛоггер.СоздатьОбъект(ИерархияСобытия, ИмяКлиента);
	ПараметрыМетода = фмКонструкторыКлиентСервер.МассивЗначений(ИмяСобытия, Сообщение, Данные);
	ОбщегоНазначения.ВыполнитьМетодОбъекта(Логгер, ИмяМетода, ПараметрыМетода);
	
КонецПроцедуры

#КонецОбласти