
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Прочитать значение параметра.
//
// Параметры:
//  ИмяПараметра		- Строка - имя параметра.
//  ЗначениеПоУмолчанию	- ОпределяемыйТип.фмЗначениеПараметра, Неопределено - значение по умолчанию.
//  ОбластьПрименения	- ОпределяемыйТип.фмОбластьПримененияПараметра, Неопределено - переопределить область применения.
// 
// Возвращаемое значение:
//  ОпределяемыйТип.фмЗначениеПараметра - значение параметра.
//
Функция ПрочитатьЗначениеПараметра(Знач ИмяПараметра, Знач ЗначениеПоУмолчанию = Неопределено, ОбластьПрименения = Неопределено) Экспорт
	
	Шаблон = ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(ВРег(ИмяКомпьютера()));
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ОбластьПрименения", ОбластьПримененияДляЗапроса(ОбластьПрименения));
	Запрос.УстановитьПараметр("ИмяПараметра",  ИмяПараметра);
	Запрос.УстановитьПараметр("Шаблон", "%" + Шаблон + "%");
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	фмПараметры.ЗначениеПараметра КАК ЗначениеПараметра
	|ИЗ
	|	РегистрСведений.фмПараметры КАК фмПараметры
	|ГДЕ
	|	фмПараметры.ИмяПараметра = &ИмяПараметра
	|	И фмПараметры.ОбластьПрименения В(&ОбластьПрименения)
	|	И ВЫБОР
	|			КОГДА фмПараметры.ИмяКомпьютера = """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ фмПараметры.ИмяКомпьютера ПОДОБНО &Шаблон СПЕЦСИМВОЛ ""~""
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	фмПараметры.ОбластьПрименения УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ЗначениеПараметра;
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбластьПримененияДляЗапроса(Знач ОбластьПрименения)
	
	ПараметрОбластьПрименения = Новый Массив();
	
	Если ОбластьПрименения = Неопределено Тогда
		ПараметрОбластьПрименения.Добавить(фмПараметрыПовтИсп.ТекущаяОбластьПримененияПараметра());
	Иначе
		ПараметрОбластьПрименения.Добавить(ОбластьПрименения);
	КонецЕсли;
	
	Для Каждого Элемент Из фмПараметрыПовтИсп.ПустоеЗначениеОбластьПрименения() Цикл
		ПараметрОбластьПрименения.Добавить(Элемент);
	КонецЦикла;
	
	Возврат ПараметрОбластьПрименения;
	
КонецФункции

#КонецОбласти

#КонецЕсли