
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Прочитать значение параметра.
//
// Параметры:
//  ИмяПараметра        - Строка - имя параметра.
//  ЗначениеПоУмолчанию - ОпределяемыйТип.фмЗначениеПараметра - значение по умолчанию.
//  ОбластьПрименения   - ОпределяемыйТип.фмОбластьПримененияПараметра - область применения параметра.
// 
// Возвращаемое значение:
//  ОпределяемыйТип.фмЗначениеПараметра - значение параметра.
//
Функция ПрочитатьЗначениеПараметра(Знач ИмяПараметра, Знач ЗначениеПоУмолчанию = Неопределено, Знач ОбластьПрименения = Неопределено) Экспорт
	
	Если ОбластьПрименения <> Неопределено Тогда
		ПараметрОбластьПрименения = ОбластьПрименения;
	Иначе
		ПараметрОбластьПрименения = фмПараметрыКлиентСерверПовтИсп.ТекущаяОбластьПримененияПараметра();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбластьПрименения", ПараметрОбластьПрименения);
	Запрос.УстановитьПараметр("ИмяПараметра",  ИмяПараметра);
	Запрос.УстановитьПараметр("ИмяКомпьютера", ВРег(ИмяКомпьютера()));
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	фмПараметры.ЗначениеПараметра КАК ЗначениеПараметра
	               |ИЗ
	               |	РегистрСведений.фмПараметры КАК фмПараметры
	               |ГДЕ
	               |	фмПараметры.ИмяПараметра = &ИмяПараметра
	               |	И (фмПараметры.ОбластьПрименения = &ОбластьПрименения
	               |			ИЛИ фмПараметры.ОбластьПрименения = НЕОПРЕДЕЛЕНО)
	               |	И ВЫБОР
	               |			КОГДА фмПараметры.ИмяКомпьютера = """"
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ фмПараметры.ИмяКомпьютера ПОДОБНО ""%"" + &ИмяКомпьютера + ""%""
	               |		КОНЕЦ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	фмПараметры.ОбластьПрименения УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ЗначениеПараметра;
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

#КонецОбласти

#КонецЕсли