
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Прочитать значение параметра.
//
// Параметры:
//  ИмяПараметра        - Строка - имя параметра.
//  ЗначениеПоУмолчанию - ОпределяемыйТип.фмЗначениеПараметра - значение по умолчанию.
// 
// Возвращаемое значение:
//  ОпределяемыйТип.фмЗначениеПараметра - значение параметра.
//
Функция ПрочитатьЗначениеПараметра(Знач ИмяПараметра, Знач ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Шаблон = ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(ВРег(ИмяКомпьютера()));
	ПараметрОбластьПрименения = Новый Массив();
	ПараметрОбластьПрименения.Добавить(фмПараметрыПовтИсп.ТекущаяОбластьПримененияПараметра());
	Для Каждого Элемент Из фмПараметрыПовтИсп.ПустоеЗначениеОбластьПрименения() Цикл
		ПараметрОбластьПрименения.Добавить(Элемент);
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ОбластьПрименения", ПараметрОбластьПрименения);
	Запрос.УстановитьПараметр("ИмяПараметра",  ИмяПараметра);
	Запрос.УстановитьПараметр("Шаблон", "%" + Шаблон + "%");
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	фмПараметры.ЗначениеПараметра КАК ЗначениеПараметра
	|ИЗ
	|	РегистрСведений.фмПараметры КАК фмПараметры
	|ГДЕ
	|	фмПараметры.ИмяПараметра = &ИмяПараметра
	|	И фмПараметры.ОбластьПрименения В(&ОбластьПрименения)
	|	И ВЫБОР
	|			КОГДА фмПараметры.ИмяКомпьютера = """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ фмПараметры.ИмяКомпьютера ПОДОБНО &Шаблон СПЕЦСИМВОЛ ""~""
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	фмПараметры.ОбластьПрименения УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ЗначениеПараметра;
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

#КонецОбласти

#КонецЕсли