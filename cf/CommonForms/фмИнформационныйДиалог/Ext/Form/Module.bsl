
#Область ОписаниеПеременных

&НаКлиенте
Перем КомандаКодВозврата;

&НаКлиенте
Перем КнопкаПоУмолчанию;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнициализацияПеременных();
	
	Если Параметры.РежимДиалога = Строка(РежимДиалогаВопрос.ДаНет) Тогда
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаДаНет;
		
		Если Параметры.КнопкаПоУмолчанию = КодВозвратаДиалога.Да Тогда
			КнопкаПоУмолчанию = КомандаКодВозврата.Получить(Строка(КодВозвратаДиалога.Да));
		Иначе
			КнопкаПоУмолчанию = КомандаКодВозврата.Получить(Строка(КодВозвратаДиалога.Нет));
		КонецЕсли;
		
	ИначеЕсли Параметры.РежимДиалога = Строка(РежимДиалогаВопрос.ОК) Тогда
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаОК;
		КнопкаПоУмолчанию = КомандаКодВозврата.Получить(Строка(КодВозвратаДиалога.ОК));
		
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не поддерживается в данном режиме", , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ТекущийЭлемент = КнопкаПоУмолчанию;
	
	Если Параметры.Ошибка Тогда
		
		// Красные оттенки
		ЦветФона1  = "FE8E8E";
		ЦветФона2  = "FFCCCC";
		ЦветФона3  = "FFFFFF";
		ЦветТекста = "740000";
		
	Иначе
		
		// Желтые оттенки
		ЦветФона1  = "fef78E";
		ЦветФона2  = "FFFFCC";
		ЦветФона3  = "FFFFFF";
		ЦветТекста = "745400";
		
	КонецЕсли;
	
	Строки = Новый Массив;
	
	Строки.Добавить("<HTML>
					| <BODY topmargin=""0"" leftmargin=""0"" rightmargin=""0"" marginheight=""0"" marginwidth=""0"" scroll=no>
					| <TABLE width=100% border=0 cellspacing=0 cellpadding=5>");
	
	Строки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<TR><td 
																			|bgcolor=""#%1"" 
																			|style=""FONT-WEIGHT: bold;
																			|FONT-SIZE: 23px; COLOR: #%2;
																			|LINE-HEIGHT: 150%;
																			|FONT-FAMILY: tahoma, arial, sans-serif"">%3</TD></TR>",
																			ЦветФона1,
																			ЦветТекста,
																			Параметры.ТекстЗаголовка));
	
	Если Не ПустаяСтрока(Параметры.Описание) Тогда
		Строки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<TR><td 
																				|bgcolor=""#%1"" 
																				|style=""FONT-WEIGHT: bold;
																				|FONT-SIZE: 15px; COLOR: #%2;
																				|LINE-HEIGHT: 150%;
																				|FONT-FAMILY: tahoma, arial, sans-serif"">%3</TD></TR>",
																				ЦветФона2,
																				ЦветТекста,
																				СтрЗаменить(Параметры.Описание, Символы.ПС, "<BR>")));
	КонецЕсли;
	
	Если Не ПустаяСтрока(Параметры.ПодробноеОписание) Тогда
		Строки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<TR><td 
																				|bgcolor=""#%1"" 
																				|style=""FONT-WEIGHT: normal;
																				|FONT-SIZE: 11px; COLOR: #%2;
																				|LINE-HEIGHT: 150%;
																				|FONT-FAMILY: tahoma, arial, sans-serif"">%3</TD></TR>",
																				ЦветФона3,
																				ЦветТекста,
																				СтрЗаменить(Параметры.ПодробноеОписание, Символы.ПС, "<BR>")));
	КонецЕсли;
	
	ТекстHTML = СтрСоединить(Строки, Символы.ПС) + "</TABLE></BODY></HTML>";
	
	Если Параметры.Таймаут > 0 Тогда
		ПодключитьОбработчикОжидания("ОкончаниеТаймаута", Параметры.Таймаут, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Команда(Команда)
	
	Результат = КомандаКодВозврата.Получить(Команда);
	
	Если Результат = Неопределено Тогда
		
		Если Команда.Имя = "Команда1" Или Команда.Имя = "КомандаNum1" Тогда
			
			Если Параметры.РежимДиалога = Строка(РежимДиалогаВопрос.ДаНет) Тогда
				Результат = КодВозвратаДиалога.Да;
			ИначеЕсли Параметры.РежимДиалога = Строка(РежимДиалогаВопрос.ОК) Тогда
				Результат = КодВозвратаДиалога.ОК;
			КонецЕсли;
			
		ИначеЕсли Команда.Имя = "Команда2" Или Команда.Имя = "КомандаNum2" Тогда
			
			Если Параметры.РежимДиалога = Строка(РежимДиалогаВопрос.ДаНет) Тогда
				Результат = КодВозвратаДиалога.Нет;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьВБуфер(Команда)
	
	Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("[Заголовок]: %1%4[Описание]: %2%4[ПодробноеОписание]: %3",
	                                                                    Параметры.ТекстЗаголовка,
	                                                                    Параметры.Описание,
	                                                                    Параметры.ПодробноеОписание,
	                                                                    "," + Символы.ПС);
	
	ParentWindow = Элементы.ТекстHTML.Документ.ParentWindow;
	ParentWindow.ClipboardData.SetData("Text", Сообщение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнициализацияПеременных()
	
	КомандаКодВозврата = Новый Соответствие;
	КомандаКодВозврата.Вставить(Команды.КомандаДа, КодВозвратаДиалога.Да);
	КомандаКодВозврата.Вставить(Строка(КодВозвратаДиалога.Да), Элементы.КнопкаДа);
	КомандаКодВозврата.Вставить(Команды.КомандаНет, КодВозвратаДиалога.Нет);
	КомандаКодВозврата.Вставить(Строка(КодВозвратаДиалога.Нет), Элементы.КнопкаНет);
	КомандаКодВозврата.Вставить(Команды.КомандаОк, КодВозвратаДиалога.ОК);
	КомандаКодВозврата.Вставить(Строка(КодВозвратаДиалога.ОК), Элементы.КнопкаОк);
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеТаймаута()
	Команда(КнопкаПоУмолчанию);
КонецПроцедуры

#КонецОбласти
