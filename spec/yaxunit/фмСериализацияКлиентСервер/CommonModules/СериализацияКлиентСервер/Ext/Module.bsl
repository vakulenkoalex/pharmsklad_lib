
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.УдалениеТестовыхДанных()
		.ДобавитьСерверныйТест("СохранитьОбъектXDTOВJson", "Метод модуля для сохранения XDTO в JSON")
		.ДобавитьСерверныйТест("ПлатформаСохраняетОбъектXDTOВJson", "Метод платформы для сохранения XDTO в JSON")
		.ДобавитьСерверныйТест("ВосстановитьОбъектXDTOИзJson_ОшибкаЕслиНеНашлиТип", "Ошибка при восстановлении отсутствующего типа")
		.ДобавитьСерверныйТест("ВосстановитьОбъектXDTOИзJson_Успех", "Восстановление XDTO из JSON")
		.ДобавитьСерверныйТест("ВосстановитьОбъектXDTOИзJson_ОшибкаЕслиПередалиНеТоЗначение", "Ошибка при восстановлении неправильного объекта");
	
КонецПроцедуры

#КонецОбласти

#Область Тесты

Процедура СохранитьОбъектXDTOВJson() Экспорт
	
	Объект = СоздатьОбъект();
	Текст = фмСериализацияКлиентСервер.СохранитьОбъектXDTOВJson(Объект);
	
	ЮТест
		.ОжидаетЧто(Текст, "результат")
		.Содержит("""ЧислоСложныйТип"": 10.5", "выгружен ЧислоСложныйТип");
	
КонецПроцедуры

Процедура ПлатформаСохраняетОбъектXDTOВJson() Экспорт
	
	Объект = СоздатьОбъект();
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
	ФабрикаXDTO.ЗаписатьJSON(ЗаписьJSON, Объект);
	Текст = ЗаписьJSON.Закрыть();
	
	ЮТест
		.ОжидаетЧто(Текст, "результат")
		.НеСодержит("""ЧислоСложныйТип"": 10.5", "выгружен ЧислоСложныйТип");
	
КонецПроцедуры

Процедура ВосстановитьОбъектXDTOИзJson_ОшибкаЕслиНеНашлиТип() Экспорт
	
	ПараметрыМетода = ОбщегоНазначенияКлиентСервер.МассивЗначений("{""list"":[]}", "new", "example");
	
	ЮТест
		.ОжидаетЧто(фмСериализацияКлиентСервер, "модуль")
		.Метод("ВосстановитьОбъектXDTOИзJson", ПараметрыМетода)
		.ВыбрасываетИсключение("Тип XDTO не найден:");
	
КонецПроцедуры

Процедура ВосстановитьОбъектXDTOИзJson_Успех() Экспорт
	
	Данные = фмСериализацияКлиентСервер.ВосстановитьОбъектXDTOИзJson("http://www.sample-package.org", "НовыйОбъект", "{""ЧислоСложныйТип"": 10.2}");
	
	ЮТест
		.ОжидаетЧто(Данные, "результат")
		.ИмеетТип(Тип("ОбъектXDTO"));
	
	ЮТест
		.ОжидаетЧто(Данные.ЧислоСложныйТип, "свойство ЧислоСложныйТип")
		.Равно(10.2);
	
КонецПроцедуры

Процедура ВосстановитьОбъектXDTOИзJson_ОшибкаЕслиПередалиНеТоЗначение() Экспорт
	
	ПараметрыМетода = ОбщегоНазначенияКлиентСервер.МассивЗначений("{""ЧислоСложныйТип"": ""вввв""}", "НовыйОбъект", "http://www.sample-package.org");
	
	ЮТест
		.ОжидаетЧто(фмСериализацияКлиентСервер, "модуль")
		.Метод("ВосстановитьОбъектXDTOИзJson", ПараметрыМетода)
		.ВыбрасываетИсключение("Ошибка проверки данных XDTO:");
	
КонецПроцедуры

#КонецОбласти

#Область События

Процедура ПередВсемиТестами() Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьОбъект()
	
	ТипОбъект = ФабрикаXDTO.Тип("http://www.sample-package.org", "Объект");
	Объект = ФабрикаXDTO.Создать(ТипОбъект);
	Объект.Строка = "1";
	Объект.ЧислоDecimal = 10.5;
	Объект.ЧислоСложныйТип = 10.5;
	
	Возврат Объект;
	
КонецФункции

#КонецОбласти