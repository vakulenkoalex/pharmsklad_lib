
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьСерверныйТест("ОшибкаПриСериализацииСообщения", "Если произошла ошибка при сериализации сообщения, то пишем представление сообщения, а не просто ошибку");
	
КонецПроцедуры

#КонецОбласти

#Область Тесты

Процедура ОшибкаПриСериализацииСообщения() Экспорт
	
	ИмяФайла = ПолучитьИмяВременногоФайла("txt");
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ИмяПараметра = "ЛоггерПутьКФайлу";
	Запись.ЗначениеПараметра = ИмяФайла;
	Запись.Записать();
	
	ОбработкаОбъект = фмЛоггерКлиентСервер.СоздатьЛоггер("Тест");
	ОбработкаОбъект.ВключитьСообщенияПользователю();
	
	Мокито
		.Обучение(ОбработкаОбъект)
		.Когда("JSON")
		.ВыброситьИсключение("ТестоваяОшибка")
		.Прогон();
	
	Данные = Новый Структура("Объект", "новый");
	ОбработкаОбъект.Информация("Событие", "Сообщение", Данные);
	
	Мокито
		.Проверить(ОбработкаОбъект)
		.КоличествоВызовов("JSON")
		.Равно(2);
	
	УдалитьФайлы(ИмяФайла);
	
	ЮТест
		.ОжидаетЧто(фмОбработкаСообщенийПользователю.СообщенияПользователюВСтроку(), "сообщения пользователю")
		.Содержит("Ошибка сериализации")
		.Содержит("ТестоваяОшибка")
		.Содержит("Данные: ");
	
КонецПроцедуры

#КонецОбласти

#Область События

Процедура ПередВсемиТестами() Экспорт
	
КонецПроцедуры

#КонецОбласти