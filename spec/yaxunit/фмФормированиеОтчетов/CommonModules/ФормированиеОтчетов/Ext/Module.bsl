
#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.УдалениеТестовыхДанных()
		.ДобавитьСерверныйТест("ЗагрузитьВариантОтчетаВариантНайден", "Загрузить вариант отчета")
		.ДобавитьСерверныйТест("ЗагрузитьВариантОтчетаВариантНеНайден", "Ошибка при загрузке варианта отчета которой нет")
		.ДобавитьСерверныйТест("РезультатОтчетаВТаблицу", "Вывод отчета в таблицу значений")
		.ДобавитьСерверныйТест("РезультатОтчетаВТаблицуСОтбором", "Вывод отчета с отбором в таблицу значений");
	
КонецПроцедуры

#КонецОбласти

#Область Тесты

Процедура ЗагрузитьВариантОтчетаВариантНайден() Экспорт
	
	ОтчетОбъект = Отчеты.фмф_Тестирование.Создать();
	фмФормированиеОтчетов.ЗагрузитьВариантОтчета(ОтчетОбъект, "Второй");
	
	Настройки = ОтчетОбъект.КомпоновщикНастроек.Настройки;
	
	ЮТест
		.ОжидаетЧто(Настройки.Выбор.Элементы, "выбранные поля настройки")
		.ИмеетДлину(1);
		
	ПроверкаВыбора = ЮТест.Предикат()
						.Реквизит("Поле").Равно(Новый ПолеКомпоновкиДанных("Поле2"))
						.Реквизит("Использование").Равно(Истина);
	ЮТест
		.ОжидаетЧто(Настройки.Выбор.Элементы[0], "выбранное поле")
		.СоответствуетПредикату(ПроверкаВыбора);

КонецПроцедуры

Процедура ЗагрузитьВариантОтчетаВариантНеНайден() Экспорт
	
	ОтчетОбъект = Отчеты.фмф_Тестирование.Создать();
	
	ЮТест
		.ОжидаетЧто(фмФормированиеОтчетов, "загрузить вариант отчета")
		.Метод("ЗагрузитьВариантОтчета", ОбщегоНазначенияКлиентСервер.МассивЗначений("Другой", ОтчетОбъект))
		.ВыбрасываетИсключение("Не найден вариант отчета");
		
КонецПроцедуры

Процедура РезультатОтчетаВТаблицу() Экспорт
	
	ОтчетОбъект = Отчеты.фмф_Тестирование.Создать();
	Таблица = фмФормированиеОтчетов.РезультатОтчетаВТаблицу(ОтчетОбъект, "Первый");
	
	ЮТест
		.ОжидаетЧто(Таблица, "результат")
		.ИмеетТип(Тип("ТаблицаЗначений"));
	ЮТест
		.ОжидаетЧто(Таблица.Количество(), "количество строк")
		.Равно(1);
	ЮТест
		.ОжидаетЧто(Таблица.Колонки.Количество(), "количество колонок")
		.Равно(1);
	
КонецПроцедуры

Процедура РезультатОтчетаВТаблицуСОтбором() Экспорт
	
	ОтчетОбъект = Отчеты.фмф_Тестирование.Создать();
	Таблица = фмФормированиеОтчетов.РезультатОтчетаВТаблицу(ОтчетОбъект, "Первый", фмКонструкторыКлиентСервер.СоздатьСоответствие("Поле1", 2));
	
	ЮТест
		.ОжидаетЧто(Таблица, "результат")
		.ИмеетТип(Тип("ТаблицаЗначений"));
	ЮТест
		.ОжидаетЧто(Таблица.Количество(), "количество строк")
		.Равно(0);
	ЮТест
		.ОжидаетЧто(Таблица.Колонки.Количество(), "количество колонок")
		.Равно(1);
	
КонецПроцедуры

#КонецОбласти

#Область События

Процедура ПередВсемиТестами() Экспорт
	
КонецПроцедуры

#КонецОбласти