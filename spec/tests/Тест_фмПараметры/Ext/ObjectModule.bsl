
// BSLLS:BeginTransactionBeforeTryCatch-off
// BSLLS:DuplicateStringLiteral-off
// BSLLS:MagicNumber-off
// BSLLS:PairingBrokenTransaction-off
// BSLLS:PublicMethodsDescription-off
// BSLLS:UsingServiceTag-off
// BSLLS:WrongUseOfRollbackTransactionMethod-off

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем КонтекстЯдра; // Контекст проигрывателя тестов
Перем Ожидаем; // Контекст плагина с методами проверки утверждений

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область xddTestRunner

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	
	НаборТестов.Добавить("Тест_ЕслиНеУказалиОбластьИНеНашлиПараметрТоВозвращаемЗначениеПоУмолчанию");
	НаборТестов.Добавить("Тест_ЕслиНеУказалиОбластьИНашлиПараметрТоВозвращаемНайденноеЗначение");
	НаборТестов.Добавить("Тест_ЕслиУказалиОбластьИОбластьСовпадаетТоВозвращаемНайденноеЗначение");
	НаборТестов.Добавить("Тест_ЕслиУказалиОбластьИОбластьНеСовпадаетИУказанПараметраДляОбластиРавнойНеопределеноТоВозвращаемЗначениеДляОбластиРавнойНеопределено");
	НаборТестов.Добавить("Тест_ЕслиУказалиОбластьИОбластьНеСовпадаетИНетПараметраДляОбластиРавнойНеопределеноТоВозвращаемЗначениеПоУмолчанию");
	НаборТестов.Добавить("Тест_ЕслиУказалиИмяКомпьютераИИмяКомпьютераСовпадаетТоВозвращаемНайденноеЗначение");
	НаборТестов.Добавить("Тест_ЕслиУказалиИмяКомпьютераИИмяКомпьютераНеСовпадаетТоВозвращаемЗначениеПоУмолчанию");
	
КонецПроцедуры

#КонецОбласти

#Область ЮнитТесты

#Область ОбщиеПроцедурыТестов

Процедура ПередЗапускомТеста() Экспорт
	
	НачатьТранзакцию();
	ПараметрыСеанса.фмТекущаяОбластьПримененияПараметра = "1";
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
	Если ТранзакцияАктивна() Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОписаниеТестов

Процедура Тест_ЕслиНеУказалиОбластьИНеНашлиПараметрТоВозвращаемЗначениеПоУмолчанию() Экспорт
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1);
	Ожидаем.Что(Результат, "результат функции").Равно(1);
	
КонецПроцедуры

Процедура Тест_ЕслиНеУказалиОбластьИНашлиПараметрТоВозвращаемНайденноеЗначение() Экспорт
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 2;
	Запись.Записать();
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1);
	Ожидаем.Что(Результат, "результат функции").Равно(2);
	
КонецПроцедуры

Процедура Тест_ЕслиУказалиОбластьИОбластьСовпадаетТоВозвращаемНайденноеЗначение() Экспорт
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ОбластьПрименения = "й";
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 2;
	Запись.Записать();
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1, "й");
	Ожидаем.Что(Результат, "результат функции").Равно(2);
	
КонецПроцедуры

Процедура Тест_ЕслиУказалиОбластьИОбластьНеСовпадаетИУказанПараметраДляОбластиРавнойНеопределеноТоВозвращаемЗначениеДляОбластиРавнойНеопределено() Экспорт
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 2;
	Запись.Записать();
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ОбластьПрименения = "ц";
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 3;
	Запись.Записать();
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1, "й");
	Ожидаем.Что(Результат, "результат функции").Равно(2);
	
КонецПроцедуры

Процедура Тест_ЕслиУказалиОбластьИОбластьНеСовпадаетИНетПараметраДляОбластиРавнойНеопределеноТоВозвращаемЗначениеПоУмолчанию() Экспорт
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ОбластьПрименения = "ц";
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 3;
	Запись.Записать();
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1, "й");
	Ожидаем.Что(Результат, "результат функции").Равно(1);
	
КонецПроцедуры

Процедура Тест_ЕслиУказалиИмяКомпьютераИИмяКомпьютераСовпадаетТоВозвращаемНайденноеЗначение() Экспорт
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 2;
	Запись.ИмяКомпьютера = ИмяКомпьютера();
	Запись.Записать();
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1);
	Ожидаем.Что(Результат, "результат функции").Равно(2);
	
КонецПроцедуры

Процедура Тест_ЕслиУказалиИмяКомпьютераИИмяКомпьютераНеСовпадаетТоВозвращаемЗначениеПоУмолчанию() Экспорт
	
	Запись = РегистрыСведений.фмПараметры.СоздатьМенеджерЗаписи();
	Запись.ИмяПараметра = "тест";
	Запись.ЗначениеПараметра = 2;
	Запись.ИмяКомпьютера = "123";
	Запись.Записать();
	
	Результат = РегистрыСведений.фмПараметры.ПрочитатьЗначениеПараметра("тест", 1);
	Ожидаем.Что(Результат, "результат функции").Равно(1);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
